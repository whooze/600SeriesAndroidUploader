import org.ajoberstar.grgit.Grgit
import groovy.json.JsonSlurper

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.28.0'
        classpath "io.realm:realm-gradle-plugin:5.10.0"
        classpath 'org.ajoberstar:grgit:1.5.0'
        classpath 'org.codehaus.groovy:groovy-json:2.4.12'
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

// change dev to false for release version (release uses the generated version name and code)
def dev = true
def devName = "arecibo 0.7.0 (beta 0.7)"

def generateVersionCode() {
    // We need to get the number of releases directly from GitHub, since Travis
    // does a "git clone --depth=50", meaning we don't get all the tags locally.
    String url = "https://api.github.com/repos/pazaan/600SeriesAndroidUploader/tags"
    def json = new JsonSlurper().parseText(url.toURL().text)

    def numOfTags = json.size()
    logger.warn("Generated Version Code: " + numOfTags)
    return numOfTags
}

def generateVersionName() {
    ext.repo = Grgit.open()

    def tag = ext.repo.describe().replaceFirst("^v", "")
    logger.warn("Generated Version Name: " + tag)
    return tag
}

def getBugfenderApiKey() {
    String bugfenderApiKey = System.getenv("BUGFENDER_API_KEY")

    if (bugfenderApiKey == null) {
        File file = new File("app/bugfender.properties")
        if (file.exists()) {
            Properties properties = new Properties()
            properties.load(new FileInputStream(file.getAbsolutePath().toString()))
            bugfenderApiKey = properties.getProperty("apiKey", "")
        } else {
            logger.warn("Bugfender API key not set")
            bugfenderApiKey = ""
        }
    }

    return "\"" + bugfenderApiKey + "\""
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    defaultConfig {
        applicationId "info.nightscout.android"
        minSdkVersion 14
        targetSdkVersion 28
        versionName = dev ? devName : generateVersionName()
        versionCode = generateVersionCode()
        buildConfigField "String", "BUGFENDER_API_KEY", getBugfenderApiKey()
    }

    buildTypes {
        release {
            minifyEnabled false
            // Set to true once we set up proguard-rules without breaking anything
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            ext.enableCrashlytics = file("fabric.properties").exists()
        }

        debug {
            ext.enableCrashlytics = file("fabric.properties").exists()
        }
    }

    lintOptions {
        // Dag nabbit :(
        // Because of http://stackoverflow.com/questions/35492259/lint-error-on-okio
        warning 'InvalidPackage'
    }
}

release {
    tagTemplate = 'v${version}'
    buildTasks = ['build']
    beforeReleaseBuild.dependsOn 'clean'
}

dependencies {
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.3'

    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    implementation 'com.bugfender.sdk:android:0.8.4'

    implementation files('libs/slf4j-api-1.7.2.jar')
    implementation files('libs/lzo-core-1.0.5.jar')

    implementation 'com.getpebble:pebblekit:4.0.1@aar'

    implementation 'com.github.thorbenprimke:realm-recyclerview:0.9.25'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v13:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'

    implementation 'org.apache.commons:commons-lang3:3.4'

    implementation 'uk.co.chrisjenx:calligraphy:2.2.0'

    // due to bug in GraphView v4.2.1 using setNumHorizontalLabels reverted to using v4.0.1
    implementation 'com.jjoe64:graphview:4.0.1'

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

    // AppUpdater v2.7 uses com.squareup.okhttp3:okhttp:3.10.0
    implementation 'com.github.javiersantos:AppUpdater:2.7'

    // The version of okhttp3 *must* be the same as the version in AppUpdater
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    implementation('com.mikepenz:materialdrawer:5.2.9@aar') {
        transitive = true
    }
    implementation 'com.mikepenz:google-material-typeface:3.0.1.1.original@aar'
    implementation 'com.mikepenz:ionicons-typeface:2.0.1.3@aar'
}

apply from: "signing.gradle"
